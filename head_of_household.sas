/*HEADER---------------------------------------------------------------------------------------------------------
|MACRO: 		HEAD_OF_HOUSEHOLD.SAS
|
|PURPOSE: 		TO DETERMINE HEAD OF HOUSEHOLD.
|
|INPUT:			TBL_NM_IN (ORACLE TABLE)
|
|LOGIC:         HOUSEHOLD WILL BE DETERMINED BY MATCHING ON MEMBER LAST NAME, ADDRESS1, ADDRESS2, CITY, STATE, ZIP
|				IN ORDER FOR ONE MAILING TO BE SENT TO THE HOUSEHOLD.						
|						
|OUTPUT			TBL_NM_OUT(ORACLE TABLE/DATASET)IS CREATED.
|
| EXAMPLE CALL: %HEAD_OF_HOUSEHOLD(TBL_NM_IN = &ORA_TMP..TMP1_RETAIL_CLAIMS_&INITIATIVE_ID._&PROGRAM_ID.&ADJ_ENGINE.,
					               TBL_NM_OUT = &ORA_TMP..RTM_LIST_&INITIATIVE_ID._&PROGRAM_ID._&ADJ_ENGINE.);
|+-----------------------------------------------------------------------------------------------------------------
|HISTORY: 
|			    SY 30OCT2008 - Hercules Version  2.1.2.01
+-----------------------------------------------------------------------------------------------------------HEADER*/
%MACRO HEAD_OF_HOUSEHOLD(TBL_NM_IN =,
					     TBL_NM_OUT =,
						 ADJ =);

%LET ERR_FL = 0;

PROC SQL;
CREATE TABLE &TABLE_PREFIX._&PROGRAM_ID._&ADJ. AS
SELECT A.*, 
	   SUBSTR(A.MBR_ID,1,9) AS MBR_ID9,
	   INPUT(A.SRC_SUFFX_PRSN_CD, 3.) AS MBR_SUFX
FROM &TBL_NM_IN. A;
QUIT;

PROC SORT DATA = &TABLE_PREFIX._&PROGRAM_ID._&ADJ. OUT = &TABLE_PREFIX._&PROGRAM_ID._&ADJ._SORTED;
	BY MBR_LAST_NM ADDR_LINE1_TXT ADDR_LINE2_TXT ADDR_CITY_NM ADDR_ST_CD ADDR_ZIP_CD DESCENDING MBR_SUFX;
RUN;

DATA &TABLE_PREFIX._&PROGRAM_ID._&ADJ._HOUSEHOLD;
	SET &TABLE_PREFIX._&PROGRAM_ID._&ADJ._SORTED;
	BY MBR_LAST_NM ADDR_LINE1_TXT ADDR_LINE2_TXT ADDR_CITY_NM ADDR_ST_CD ADDR_ZIP_CD;
	IF LAST.ADDR_ZIP_CD;
RUN;

DATA &TABLE_PREFIX._&PROGRAM_ID._&ADJ._HOUSEHOLD (RENAME = (CDH_BENEFICIARY_ID2 = CDH_BENEFICIARY_ID));
SET &TABLE_PREFIX._&PROGRAM_ID._&ADJ._HOUSEHOLD;
	CDH_BENEFICIARY_ID2 = PT_BENEFICIARY_ID;
	DROP CDH_BENEFICIARY_ID;
RUN;

%DROP_ORACLE_TABLE(TBL_NAME=&TBL_NM_OUT.);

DATA &TBL_NM_OUT;
SET &TABLE_PREFIX._&PROGRAM_ID._&ADJ._HOUSEHOLD;
RUN;

PROC SQL;
DROP TABLE &TABLE_PREFIX._&PROGRAM_ID._&ADJ.
		  ,&TABLE_PREFIX._&PROGRAM_ID._&ADJ._SORTED
          ,&TABLE_PREFIX._&PROGRAM_ID._&ADJ._HOUSEHOLD;
QUIT;

%SET_ERROR_FL;

%ON_ERROR( ACTION=ABORT
          ,EM_TO=&PRIMARY_PROGRAMMER_EMAIL
          ,EM_SUBJECT=HCE SUPPORT: NOTIFICATION OF ABEND INITIATIVE_ID &INITIATIVE_ID
          ,EM_MSG=%STR(A PROBLEM WAS ENCOUNTERED IN THE HEAD_OF_HOUSEHOLD MACRO. PLEASE CHECK THE LOG ASSOCIATED WITH INITIATIVE_ID &INITIATIVE_ID.));

%MEND HEAD_OF_HOUSEHOLD;

