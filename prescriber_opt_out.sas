/*HEADER---------------------------------------------------------------------------------------------------------
|MACRO: 		PRESCRIBER_OPT_OUT.SAS
|
|PURPOSE: 		
|
|INPUT:			
|
|LOGIC:        						
|						
|OUTPUT:		
|+-----------------------------------------------------------------------------------------------------------------
|HISTORY: 
|			    SR 30OCT2008 - Hercules Version  2.1.2.01
+-----------------------------------------------------------------------------------------------------------HEADER*/

%MACRO PRESCRIBER_OPT_OUT(TBL_NAME_IN=,
					      TBL_NAME_OUT=);

%IF &PROGRAM_ID. EQ 105 AND &TASK_ID. EQ 11 %THEN %DO; 

%RESOLVE_CLIENT(TBL_NAME_OUT=&DB2_TMP..&TABLE_PREFIX._QL,
  				TBL_NAME_OUT_RX=&ORA_TMP..&TABLE_PREFIX._RX,
  				TBL_NAME_OUT_RE=&ORA_TMP..&TABLE_PREFIX._RE,
  				EXECUTE_CONDITION=%STR(1=1));

%IF &QL_ADJ = 1 %THEN %DO;
	%LET TBL_NAME_IN_QL = %STR(TBL_NAME_IN_QL);
    %LET TBL_NAME_IN_QL_PRES_OUT = %STR(TBL_NAME_IN_QL_PRES_OUT);
	%LET QL_CONS = %STR(IF ADJ_ENGINE = "QL" THEN OUTPUT TBL_NAME_IN_QL;);
%END;
%ELSE %DO;
	%LET TBL_NAME_IN_QL = %STR();
    %LET TBL_NAME_IN_QL_PRES_OUT = %STR();
	%LET QL_CONS = %STR();
%END;

%IF &RX_ADJ = 1 %THEN %DO;
	%LET TBL_NAME_IN_RX = %STR(TBL_NAME_IN_RX);
    %LET TBL_NAME_IN_RX_PRES_OUT = %STR(TBL_NAME_IN_RX_PRES_OUT);
	%LET RX_CONS = %STR(IF ADJ_ENGINE = "RX" THEN OUTPUT TBL_NAME_IN_RX;);
%END;
%ELSE %DO;
	%LET TBL_NAME_IN_RX = %STR();
    %LET TBL_NAME_IN_RX_PRES_OUT = %STR();
	%LET RX_CONS = %STR();
%END;

%IF &RE_ADJ = 1 %THEN %DO;
	%LET TBL_NAME_IN_RE = %STR(TBL_NAME_IN_RE);
    %LET TBL_NAME_IN_RE_PRES_OUT = %STR(TBL_NAME_IN_RE_PRES_OUT);
	%LET RE_CONS = %STR(IF ADJ_ENGINE = "RE" THEN OUTPUT TBL_NAME_IN_RE;);
%END;
%ELSE %DO;
	%LET TBL_NAME_IN_RE = %STR();
    %LET TBL_NAME_IN_RE_PRES_OUT = %STR();
	%LET RE_CONS = %STR();
%END;

DATA &TBL_NAME_IN_QL. &TBL_NAME_IN_RX. &TBL_NAME_IN_RE.;
	SET &TBL_NAME_IN.;
	&QL_CONS.
	&RX_CONS.
	&RE_CONS.
RUN;

%IF &QL_ADJ = 1 %THEN %DO;
	PROC SQL;
		CREATE TABLE TBL_NAME_IN_QL_PRES_OUT AS
		SELECT A.*
		FROM TBL_NAME_IN_QL A
		LEFT JOIN
		     &DB2_TMP..&TABLE_PREFIX._QL B
		ON 		A.CLIENT_ID = B.CLIENT_ID
		    AND INPUT(A.CLIENT_LEVEL_1,20.) = B.CLT_PLAN_GROUP_ID
		WHERE B.CLT_PLAN_GROUP_ID IS NULL;
	QUIT;
%END;

%IF &RX_ADJ = 1 %THEN %DO;
	PROC SQL;
		CREATE TABLE TBL_NAME_IN_RX_PRES_OUT AS
		SELECT A.*
		FROM TBL_NAME_IN_RX A
		LEFT JOIN
		     &ORA_TMP..&TABLE_PREFIX._RX B
		ON 		A.CLIENT_LEVEL_1 = B.CARRIER_ID
		    AND A.CLIENT_LEVEL_2 = B.ACCOUNT_ID
		    AND A.CLIENT_LEVEL_3 = B.GROUP_CD
		WHERE B.CARRIER_ID IS NULL;
	QUIT;
%END;

%IF &RE_ADJ = 1 %THEN %DO;
	PROC SQL;
		CREATE TABLE TBL_NAME_IN_RE_PRES_OUT AS
		SELECT A.*
		FROM TBL_NAME_IN_RE A
		LEFT JOIN
		     &ORA_TMP..&TABLE_PREFIX._RE B
		ON 		A.CLIENT_LEVEL_1 = B.INSURANCE_CD
		    AND A.CLIENT_LEVEL_2 = B.CARRIER_ID
		    AND A.CLIENT_LEVEL_3 = B.GROUP_CD
		WHERE B.INSURANCE_CD IS NULL;
	QUIT;
%END;

DATA &TBL_NAME_OUT.;
	SET &TBL_NAME_IN_QL_PRES_OUT.
	    &TBL_NAME_IN_RX_PRES_OUT.
		&TBL_NAME_IN_RE_PRES_OUT.;
RUN;

/*PROC SQL;*/
/*	CREATE TABLE &TBL_NAME_OUT. AS*/
/*	SELECT * FROM TBL_NAME_IN_QL_PRES_OUT*/
/*	UNION ALL*/
/*	SELECT * FROM TBL_NAME_IN_RX_PRES_OUT*/
/*	UNION ALL*/
/*	SELECT * FROM TBL_NAME_IN_RE_PRES_OUT;*/
/*QUIT;*/

%END;

%MEND PRESCRIBER_OPT_OUT;

