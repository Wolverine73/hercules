/**HEADER------------------------------------------------------------------------------------
|
| PROGRAM NAME: CHK_DOC_CLTID_FIX.SAS
|
| CALL REFERENCE: CHK_DOC_CLTID_FIX IS CALLED BY CHECK_DOCUMENT.SAS
|
| PURPOSE:
|       DETERMINES THE CLIENT_ID ASSOCIATED TO A CARRIER_ID OR AN INSURANCE_CD
|       FOR RXCLM/RECAP CLIENTS, WHEN THE CLIENT HIERARCHIES ARE NOT POPULATED
|       IN THE PENDING DATASET FOR RX/RE INITIATIVES
|
| INPUT:  
|       DATASET:
|				WORK.RXCM_DOC_OVR (RXCLM - CREATED IN CHECK_DOCUMENT.SAS)
|				WORK.RECP_DOC_OVR (RXCLM - CREATED IN CHECK_DOCUMENT.SAS)
|		TABLES :
|          		CLAIMSA.TCLIENT1
|          		DSS_CLIN.V_ALGN_LVL_DENORM (USED ONLY FOR RECAP)
|
| LOGIC:
|		- FOR RXCLM, THE CARRIER_ID POPUALTED IN DOCUMENT OVERRIDE TABLES
|         IS JOINED AGAINST THE CLIENT_CD IN CLAIMSA.TCLIENT1 TO OBTAIN
|         CLIENT_ID
|       - FOR RECAP, THE INSURANCE_CD POPULATED IN DOCUMENT OVERRIDE TABLES
|         IS JOINED AGAINST THE RPT_OPT1_CD IN DSS_CLIN.V_ALGN_LVL_DENORM
|         TO OBTAIN CARRIER_ID, AND THE CARRIER_ID IN TURN IS JOINED AGAINST 
|         THE CLIENT_CD IN CLAIMSA.TCLIENT1 TO OBTAIN CLIENT_ID
|
| OUTPUT: 
|       DATASET:
|				WORK.RXCM_DOC_OVR (RXCLM - USED IN CHECK_DOCUMENT.SAS)
|				WORK.RECP_DOC_OVR (RXCLM - USED IN CHECK_DOCUMENT.SAS)
|
|------------------------------------------------------------------------------------------
| HISTORY: 05SEP2008 - SR Hercules Version  2.1.01
|------------------------------------------------------------------------------------------
+---------------------------------------------------------------------------------*HEADER*/


%MACRO CHK_DOC_CLTID_FIX_A(ADJ_ENGINE = );

%*SASDOC -----------------------------------------------------------------------
 | RXCLM - THE CARRIER_ID POPUALTED IN DOCUMENT OVERRIDE TABLES
 |         IS JOINED AGAINST THE CLIENT_CD IN CLAIMSA.TCLIENT1 TO OBTAIN
 |         CLIENT_ID
 +-----------------------------------------------------------------------SASDOC*;


 %IF &ADJ_ENGINE. = RX %THEN %DO;

	PROC SQL;
		CREATE TABLE RXCM_DOC_OVR_TMP AS  
		SELECT DISTINCT C.CLIENT_ID, A.*
		FROM RXCM_DOC_OVR A
			,CLAIMSA.TCLIENT1 C
		WHERE TRIM(LEFT(A.CARRIER_ID)) = TRIM(LEFT(C.CLIENT_CD));
	QUIT;

	DATA RXCM_DOC_OVR;
		SET RXCM_DOC_OVR_TMP;
	RUN;

 %END;

%*SASDOC -----------------------------------------------------------------------
 | RECAP - THE INSURANCE_CD POPULATED IN DOCUMENT OVERRIDE TABLES
 |         IS JOINED AGAINST THE RPT_OPT1_CD IN DSS_CLIN.V_ALGN_LVL_DENORM
 |         TO OBTAIN CARRIER_ID, AND THE CARRIER_ID IN TURN IS JOINED AGAINST 
 |         THE CLIENT_CD IN CLAIMSA.TCLIENT1 TO OBTAIN CLIENT_ID
 +-----------------------------------------------------------------------SASDOC*;

 %IF &ADJ_ENGINE. = RE %THEN %DO;

	PROC SQL NOPRINT;
		CONNECT TO ORACLE(PATH=&GOLD );
		CREATE TABLE ALGN_LVL_LIST AS
		SELECT * FROM CONNECTION TO ORACLE
		( 
			SELECT DISTINCT A.RPT_OPT1_CD AS INSURANCE_CD
							,A.EXTNL_LVL_ID1 AS CARRIER_ID
							,A.EXTNL_LVL_ID3 AS GROUP_CD
			FROM  DSS_CLIN.V_ALGN_LVL_DENORM A
			WHERE A.SRC_SYS_CD = 'R'
			AND SYSDATE BETWEEN A.ALGN_GRP_EFF_DT AND A.ALGN_GRP_END_DT
		) ;
		DISCONNECT FROM ORACLE;
	QUIT;

	PROC SQL;
	CREATE TABLE RECP_DOC_OVR_TMP AS  
	SELECT DISTINCT C.CLIENT_ID, A.*
	FROM RECP_DOC_OVR A
	    ,ALGN_LVL_LIST B
		,CLAIMSA.TCLIENT1 C
	WHERE UPCASE(TRIM(LEFT(A.INSURANCE_CD))) = UPCASE(TRIM(LEFT(B.INSURANCE_CD)))
	 AND TRIM(LEFT('Z')) || TRIM(LEFT(B.CARRIER_ID)) = TRIM(LEFT(C.CLIENT_CD));
	QUIT;

	DATA RECP_DOC_OVR;
		SET RECP_DOC_OVR_TMP;
	RUN;

 %END;

%MEND CHK_DOC_CLTID_FIX_A;
