/*HEADER---------------------------------------------------------------------------------------------------------
|MACRO: 		PARTICIPANT_EXCLUSIONS.SAS
|
|PURPOSE:  EXCLUDES PARTICIPANTS FROM THE STD PROACTIVE REFILL AND RETAIL TO MAIL
|          MAILINGS
|
|INPUT:			
|
|LOGIC:         					
|						
|OUTPUT:		
|+-----------------------------------------------------------------------------------------------------------------
|HISTORY: 
|Hercules Version  2.1.2.01
| SR 01OCT2008 - INITIAL 
|Hercules Version  2.1.2.02
| SY 22DEC2008 - MODIFIED THE SQL. DELETED THE CONNECTION TO ORACLE
|Hercules Version  2.1.2.03
| NW 23DEC2009 - MODIFIED THE ppt exculsion table creation SQL to cast ql_beneficiary_id to a number for successful
|                removal of ppts. 
+-----------------------------------------------------------------------------------------------------------HEADER*/


%MACRO PARTICIPANT_EXCLUSIONS(TBL_NAME_IN = );

/*LIBNAME PRCT_EXC "/DATA/%LOWCASE(SAS&SYSMODE.1/HERCULES/PARTICIPANT_EXCLUSIONS)";*/
/*commented for testing purpose-Sandeep*/
LIBNAME PRCT_EXC "/herc&sysmode/data/hercules/participant_exclusions";
PROC SQL;
	SELECT COUNT(*) INTO :PT_EXC_CNT
	FROM PRCT_EXC.PARTICIPANT_EXCLUSION
    WHERE PROGRAM_ID = &PROGRAM_ID.
	  AND TODAY() BETWEEN EFFECTIVE_DT AND EXPIRATION_DT;
QUIT;

%IF &PT_EXC_CNT > 0 %THEN %DO;

	%DROP_ORACLE_TABLE(TBL_NAME = &ORA_TMP..PARTICIPANT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE.);

/* 23DEC2009 - NW -  ADDED DROP & MODIFIED SQL BELOW IT.  */
	%DROP_ORACLE_TABLE(TBL_NAME = &ORA_TMP..PPT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE.);
	PROC SQL;
		CREATE TABLE &ORA_TMP..PPT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE. AS
		SELECT * FROM PRCT_EXC.PARTICIPANT_EXCLUSION
	    WHERE PROGRAM_ID = &PROGRAM_ID.
		  AND TODAY() BETWEEN EFFECTIVE_DT AND EXPIRATION_DT;

	QUIT;

/* 23DEC2009 - NW -  ADDED THIS SQL.  */
  	PROC SQL;
  		CONNECT TO ORACLE (PATH = &GOLD.);
  		EXECUTE
  		(
		    CREATE TABLE &ORA_TMP..PARTICIPANT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE. AS  			
            SELECT PROGRAM_ID, 
                   CAST (QL_BENEFICIARY_ID as NUMBER) AS PT_BENEFICIARY_ID, 
                   MBR_GID, MBR_ID, EFFECTIVE_DT, EXPIRATION_DT, HSC_USR_ID, HSC_TS, HSU_USR_ID, HSU_TS
			FROM   &ORA_TMP..PPT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE.            
  	   	)BY ORACLE;
  		DISCONNECT FROM ORACLE;
  	QUIT;

/* 22DEC2008 - SY -  MODIFIED THE SQL. DELETED THE CONNECTION TO ORACLE */

PROC SQL NOPRINT;
  DELETE FROM &TBL_NAME_IN. A
			WHERE EXISTS ( SELECT 'X'
			               FROM &ORA_TMP..PARTICIPANT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE. B
			               WHERE A.MBR_GID = B.MBR_GID);
QUIT;

/* 22DEC2008 - SY -  MODIFIED THE SQL. DELETED THE CONNECTION TO ORACLE */
/* 23DEC2009 - NW -  MODIFIED THE SQL. DELETED THE CONNECTION TO ORACLE */
PROC SQL NOPRINT;
  DELETE FROM &TBL_NAME_IN. A
			WHERE EXISTS ( SELECT 'X'
			               FROM &ORA_TMP..PARTICIPANT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE. B
			               WHERE A.PT_BENEFICIARY_ID = B.PT_BENEFICIARY_ID );
QUIT;
/*WHERE TRIM(LEFT(PUT(A.PT_BENEFICIARY_ID,$CHAR40.))) = TRIM(LEFT(B.QL_BENEFICIARY_ID)));*/

/* 22DEC2008 - SY -  MODIFIED THE SQL. DELETED THE CONNECTION TO ORACLE */

PROC SQL NOPRINT;
  DELETE FROM &TBL_NAME_IN. A
			WHERE EXISTS ( SELECT 'X'
			               FROM &ORA_TMP..PARTICIPANT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE. B
			               WHERE TRIM(LEFT(TRIM(RIGHT(A.MBR_ID)))) = TRIM(LEFT(TRIM(RIGHT(B.MBR_ID)))));
QUIT;

	%DROP_ORACLE_TABLE(TBL_NAME = &ORA_TMP..PARTICIPANT_EXCLUSIONS_&INITIATIVE_ID._&ADJ_ENGINE.);

%END;

%MEND PARTICIPANT_EXCLUSIONS;



